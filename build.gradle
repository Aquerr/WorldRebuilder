import org.spongepowered.gradle.plugin.config.PluginLoaders

buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'org.spongepowered.gradle.plugin' version '2.1.1'
    id 'org.spongepowered.gradle.ore' version '2.1.1' // for Ore publishing
}

apply plugin: 'net.minecraftforge.gradle'

group = "io.github.aquerr"
version = "1.3.0"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'
    api 'org.spongepowered:spongeapi:8.1.0'
}

jar {
    if(System.getenv("JENKINS_HOME")) {
        version = version + "_" + System.getenv("BUILD_NUMBER")
        println("File name => " + baseName)
    } else {
        version = version + "-SNAPSHOT"
    }
}

minecraft {
    mappings channel: 'official', version: '1.16.5'
}

sponge {
    apiVersion("8.1.0")
    license("MIT")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin("worldrebuilder") {
        displayName("World Rebuilder")
        version("1.3.0")
        entrypoint("io.github.aquerr.worldrebuilder.WorldRebuilder")
        description("Rebuilds destroyed blocks after specified time.")
        links {
            homepage("https://github.com/Aquerr/WorldRebuilder")
            source("https://github.com/Aquerr/WorldRebuilder")
            issues("https://github.com/Aquerr/WorldRebuilder/issues")
        }
        contributor("Aquerr") {
            description("Lead Developer")
        }
//        dependency("spongeapi") {
//            loadOrder(PluginDependency.LoadOrder.AFTER)
//            optional(false)
//        }
    }
}

tasks.create("publishBuildOnDiscord") {
    group = "Publishing"
    description = "Task for publishing the jar file to discord's jenkins channel"
    doLast {
        String[] jarFiles = new FileNameFinder().getFileNames(project.buildDir.getPath(), "**/*.jar")

        if(jarFiles.length > 0) {
            println("Found jar files: " + jarFiles)

            String lastCommitDescription = "git log -1 --pretty=%B".execute().getText()
            if(lastCommitDescription == null || lastCommitDescription == "") {
                lastCommitDescription = "No changelog provided"
            }

            exec {
                commandLine("java", "-jar", "..\\jenkinsdiscordbot-1.0.jar", "WorldRebuilder", jarFiles[0], lastCommitDescription)
            }
        }
    }
}

jar.finalizedBy('reobfJar')

oreDeployment {
    // The default publication here is automatically configured by SpongeGradle
    // using the first-created plugin's ID as the project ID
    // A version body is optional, to provide additional information about the release
    /*
    defaultPublication {
        // Read the version body from the file whose path is provided to the changelog gradle property
        versionBody.set(providers.gradleProperty("changelog").map { file(it).readText(Charsets.UTF_8) }.orElse(""))
    }*/
}
