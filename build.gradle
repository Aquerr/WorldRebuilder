buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://sizableshrimp.me/maven"
        }
        mavenCentral()
        maven {
            url = "https://maven.minecraftforge.net"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'org.spongepowered.plugin' version '0.9.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'

group = "io.github.aquerr"
version = "1.2.0"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    api 'org.spongepowered:spongeapi:7.2.0'
    annotationProcessor 'org.spongepowered:spongeapi:7.2.0'
}

jar {
    if(System.getenv("JENKINS_HOME")) {
        version = version + "_" + System.getenv("BUILD_NUMBER")
        println("File name => " + baseName)
    } else {
        version = version + "-SNAPSHOT"
    }
}

minecraft {
    forgeVersion = '1.12.2-14.23.5.2838'
    mappings = 'stable_39'
}

tasks.create("publishBuildOnDiscord") {
    group = "Publishing"
    description = "Task for publishing the jar file to discord's jenkins channel"
    doLast {
        String[] jarFiles = new FileNameFinder().getFileNames(project.buildDir.getPath(), "**/*.jar")

        if(jarFiles.length > 0) {
            println("Found jar files: " + jarFiles)

            String lastCommitDescription = "git log -1 --pretty=%B".execute().getText()
            if(lastCommitDescription == null || lastCommitDescription == "") {
                lastCommitDescription = "No changelog provided"
            }

            exec {
                commandLine("java", "-jar", "..\\jenkinsdiscordbot-1.0.jar", "WorldRebuilder", jarFiles[0], lastCommitDescription)
            }
        }
    }
}
